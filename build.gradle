plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'eu.jrie.abacus'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_15

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.mockito:mockito-core:3.8.0'
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn integrationTest

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

shadowJar {
    manifest {
        attributes(["Main-Class": "eu.jrie.abacus.Abacus"])
    }
    archiveBaseName.set('abacus')
    archiveClassifier.set('')
    archiveVersion.set('')
}
